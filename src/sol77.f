C   25/06/84 601171421  MEMBER NAME  SOL77    (S)           FORTRAN
      SUBROUTINE SOL77(X,Y,T,IKICK,ISPLIT,TWIST,NSOL)
C
C
C
C=====ROUTINE TO SET UP 7X7 SOLENOID MATRIX TO HANDLE SHIFTED AND
C=====TILTED SOLENOIDS ACCORDING TO G.RIPKEN FORMALISM.
C=====INTERNALLY WE USE CANONICAL COORDINATES. THESE ARE IDENTICAL
C=====WITH X,X',Z,Z' OUTSIDE THE SOLENOID.
C=====ONLY MAKE A RANDOM SHIFT IF IT IS NOT 2ND PART OF A SPLIT SOLEN'D.
C=====ADD THIS SHIFT TO ANY SHIFT ALREADY READ IN.
      IMPLICIT REAL*8 (A-H,O-Z)
      DIMENSION T(7,7),TA(7,7),TB(7,7),TC(7,7),TD(7,7),TE(7,7)
      DIMENSION RSOL(3)
C
      PI=3.1415926535897932D0
C
C
C
C
C=====SX,SZ ARE TRANSVERSE TRANSLATIONS (MM) OF THE SOLENOID CENTRE
C     W.R.T. THE DESIGN ORBIT.
C=====AX,AZ ARE TILT ANGLES (MRAD) W.R.T. THE DESIGN ORBIT.
      AX=0.D0
      AZ=0.D0
      SX=0.D0
      SZ=0.D0
C=====IF TWIST NE ZERO THEN READ SOLENOID SHIFTS FROM NEXT 2 LINES.
    1 FORMAT(A1,I4,1X,A8,2F12.8)
      IF(TWIST.GT.0.D0)READ(52,1)KOM,ID,ENAME,AX,AZ
      IF(TWIST.GT.0.D0)READ(52,1)KOM,ID,ENAME,SX,SZ
C
C
C
      CALL UNIT(7,T)

      NSOLM6=NSOL-6
      IF(NSOLM6.GT.0)RETURN

      T(1,2)=Y
      T(3,4)=Y
      IF(X.EQ.0.D0)RETURN
C
C      IF(IKICK.EQ.5.AND.ISPLIT.EQ.0)CALL RANNOR(GAUS1,GAUS2)
C      IF(IKICK.EQ.5)AX=0.001D0*GAUS1+AX
C     IF(IKICK.EQ.5)AZ=0.001D0*GAUS2+AZ
      AXT=1000.*AX
      AZT=1000.*AZ
      SXT=1000.*SX
      SZT=1000.*SZ
      WRITE(53,10)SXT,SZT,AXT,AZT
   10 FORMAT(' ',' LAT. SHIFT MM. ',2F6.2, 'LAT. TILT.MRAD ',2F6.2)
C
C=====SET MATRIX FOR ENTRANCE END FIELD; USE RIPKEN H VARIABLE
      CALL UNIT(7,TA)
      H=X*0.5D0/Y
      TH=2.D0*H
      TA(2,7)= H*(SZ-0.5D0*Y*AZ)
      TA(4,7)=-H*(SX-0.5D0*Y*AX)
C
C
C=====SET UP THE MATRIX FOR THE CENTRAL FIELD
      CALL UNIT(7,TB)
      TB(5,5)=1.D0
      TB(6,6)=1.D0
      TB(7,7)=1.D0
      ANG=2.D0*H*Y
      C=DCOS(ANG)
      S=DSIN(ANG)
      TB(1,1)=0.5D0*(1.D0+C)
      TB(2,2)= TB(1,1)
      TB(3,3)= TB(1,1)
      TB(4,4)= TB(1,1)
      TB(1,2)= S*0.5D0/H
      TB(3,4)= TB(1,2)
      TB(1,3)= 0.5D0*S
      TB(2,4)= TB(1,3)
      TB(3,1)=-TB(1,3)
      TB(4,2)=-TB(1,3)
      TB(1,4)= (1.D0-C)*0.5D0/H
      TB(3,2)=-TB(1,4)
      TB(2,1)=-H*S*0.5D0
      TB(2,3)=-H*(1-C)*0.5D0
      TB(4,1)=-TB(2,3)
      TB(4,3)= TB(2,1)
C=====INHOMOGENEOUS TERMS GOING INTO COLUMN 7.
      TB(1,7)= AZ*(1.D0-C)/TH    -AX*(Y-S/TH)
      TB(2,7)=-AX*(1.D0-C)/TH    +AZ*(Y+S/TH)
      TB(3,7)=-AX*(1.D0-C)/TH    -AZ*(Y-S/TH)
      TB(4,7)= AZ*(1.D0-C)/TH    +AX*(Y+S/TH)
      TB(2,7)= H*TB(2,7)
      TB(4,7)=-H*TB(4,7)
C
C
C=====SET UP THE MATRIX FOR THE EXIT END FIELD.
      CALL UNIT(7,TC)
      TC(2,7)=-H*(SZ+0.5D0*Y*AZ)
      TC(4,7)= H*(SX+0.5D0*Y*AX)
C
C
C=====MULTIPLY IT ALL TOGETHER
      CALL JAM777(TD,TB,TA)
      CALL JAM777(T ,TC,TD)
C
C
C
      RETURN
      END
