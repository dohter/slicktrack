C   25/07/82 406051704  MEMBER NAME  FOCDIP   (S)           FORTRAN
      SUBROUTINE FOCDIP(NTY,J)
C
C=====GENERATES MATRICES FOR 'HALF' QUADS TO BE SLOTTED IN BETWEEN
C=====DIPOLE SLICES FOR TYPE 12/13 DIPOLES WHERE THE CURVATURE IS SMALL
C=====ENOUGH THAT THE 1/RHO**2 FOCUSSING SHOULD BE USED.
C=====A 'HALF' QUAD IS A QUAD WHICH ONLY FOCUSSES/DEFOCUSSES IN ONE
C=====PLANE SO THAT THE MATRIX FOR THE OTHER PLANE IS A UNIT MATRIX.
C=====FROM THE DIPOLE INFORMATION,THREE MAGNET TYPES ARE GENERATED--
C=====     1)THE DIPOLE SLICE
C=====     2)A FULL STRENGTH THIN 'HALF' QUAD
C=====     3)A HALF STRENGTH THIN 'HALF' QUAD
C=====
C=====IF THE DIPOLE STRENGTH IS XX FOR A 1/NUNT SLICE AND THE INPUT
C=====LENGTH IS YY THEN THE QUAD STRENGTHS ARE GENERATED FROM
C=====1/RHO = XX/YY AND AND Q=YY/RHO**2.
C=====ALL 3 GENERATED MAGNETS ARE GIVEN THE SAME NAME BUT DIFFERENT
C=====TYPE NUMBERS.
C=====WHEN THE LOGIC FINDS A FOCUSSING DIPOLE IN THE LATTICE STRUCTURE
C=====IT CALLS THE ROUTINE DIPOLE WHICH CONSTRUCTS A QUAD/DIPOLE
C=====SANDWICH--GETTING THE QUAD DATA FROM THE NEXT POSITIONS IN THE
C=====TYPE LIST.SO EVERYTHING IS ANCHORED TO THE DIPOLE
C=====THE DIPOLE IS LABELLED WITH NTWIST=-99 FOR RECOGNITION BY THE
C=====ROUTINE DIPOLE WHICH SETS UP THE LATTICE STRUCTURE.
C=====THE QUADS ARE LABELLED WITH NTWIST=-299,-999 FOR RECOGNITION
C=====IN THE FOLLOWING MATRIX MANIPULATIONS.
C
      IMPLICIT REAL*8 (A-H,O-Z)
#include "clatic.for"
#include "csol.for"
C
C
C
      YYSAV=YY(NTY)
      XXSAV=XX(NTY)
      NSAV=NAME(NTY)
      NUSAV=NUNT(NTY)
      NSOLSV=NSOL(NTY)
C     RHO=YYSAV/XXSAV -----TO AVOID DIVIDE CHECKS WITH ZERO STRENGTH
C     Q=YYSAV/RHO**2 ------DIPOLES,REARRANGE.
      Q=XXSAV**2/YYSAV
C
C=====FIRST GENERATE DIPOLE PARAMETERS.CHOOSE VERT/HOR BEND
C=====CORRECTED 14/4/83
      IF(J.EQ.12)TMAT(2,6,NTY)= XX(NTY)
      IF(J.EQ.12)TMAT(5,1,NTY)=-XX(NTY)
      IF(J.EQ.12)ID(NTY)=2
      IF(J.EQ.12)GO TO 1
C=====TO TEMPORARILY TURN OFF ONLY THE VERTICAL BENDS BUT NOT THE QUADS.
C     XX(NTY)=0
      IF(J.EQ.13)TMAT(4,6,NTY)=-XX(NTY)
      IF(J.EQ.13)TMAT(5,3,NTY)= XX(NTY)
      IF(J.EQ.13)ID(NTY)=9
    1 CONTINUE
      NAME(NTY)=NSAV
      NTWIST(NTY)=-99.
      TWIST(NTY)=-99.
      NUNT(NTY)=NUSAV
      NSOL(NTY)=NSOLSV
C
      NTY=NTY+1
C=====GENERATE 'HALF' QUAD PARAMETERS
      XX(NTY)=Q
      TMAT(2,1,NTY)=-XX(NTY)
      TMAT(4,3,NTY)=0
      NTWIST(NTY)=-299.
      TWIST(NTY)=-299.
      IF(J.EQ.12)GO TO 2
      XX(NTY)=-Q
      TMAT(2,1,NTY)=0
C=====CORRECTED: CHANGE TO MINUS SIGN 25/11/83
      TMAT(4,3,NTY)=-XX(NTY)
      NTWIST(NTY)=-999.
      TWIST(NTY)=-999.
    2 CONTINUE
      YY(NTY)=YYSAV
      ID(NTY)=3
      NAME(NTY)=NSAV
      NUNT(NTY)=1
      NSOL(NTY)=NSOLSV
C
C
      NTY=NTY+1
C=====GENERATE HALF 'HALF' QUAD PARAMETERS
      XX(NTY)=Q/2.
      TMAT(2,1,NTY)=-XX(NTY)
      TMAT(4,3,NTY)=0
      NTWIST(NTY)=-299.
      TWIST(NTY)=-299.
      IF(J.EQ.12)GO TO 3
      XX(NTY)=-Q/2.
      TMAT(2,1,NTY)=0
C=====CORRECTED: CHANGE TO MINUS SIGN 25/11/83
      TMAT(4,3,NTY)=-XX(NTY)
      NTWIST(NTY)=-999.
      TWIST(NTY)=-999.
    3 CONTINUE
      YY(NTY)=YYSAV/2
      ID(NTY)=3
      NAME(NTY)=NSAV
      NUNT(NTY)=1
      NSOL(NTY)=NSOLSV
      RETURN
      END
