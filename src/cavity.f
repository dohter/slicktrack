C   13/09/83 510021909  MEMBER NAME  CAVITY   (SEPT95.S)    FORTRAN
      SUBROUTINE CAVITY(E0,U0,RLENGE,CRAD,RADSTR)
C
C
C=====ROUTINE TO SET UP SINE AND COSINE CAVITY STRENGTH TERMS.
C=====TAKE CAVITY VOLTAGES(MEGAVOLTS) SPECIFIED IN THE VARIABLE 'XX'
C=====AT BEAM ENERGY 'ECAV' AND COMPUTE A COMMON R.F. PHASE 'PHI'.
C=====THEN WITH FIXED PHASE, SCALE THE VOLTAGES AND RADIATION LOSS TO
C=====THE CURRENT ENERGY 'E0'.
C=====STORE THE COSINE & SINE TERMS IN XX & YY RESP.(I.E. OVERWRITE XX)
C=====ASSUMING THAT THE SYNCH. FREQ. COMES OUT  CORRECTLY, AT SUBSEQUENT
C=====ENERGIES LEAVE THE COS(PHI) TERMS UNCHANGED BUT SCALE THE SIN(PHI)
C=====TERMS WITH ENERGY.
C=====THIS IS AN ATTEMPT TO SIMULATE NORMAL MACHINE RUNNING CONDITIONS.
C=====AND ESSENTIALLY RELIES ON THE FACT THAT ONCE V*COS(PHI) AND
C=====V*SIN(PHI) ARE FIXED BY THE ENERGY LOSS AND SYNCH. TUNE THEN
C=====V & PHI ARE FULLY DETERMINED, OR IN OTHER WORDS WE CAN USE
C=====V*COS(PHI) & V*SIN(PHI) AS NEW INDEPENDENT VARIABLES.
C
C
      IMPLICIT REAL*8(A-H,O-Z)
C     SAVE ICALL
C     SAVE RADLOS
      SAVE SINPHI                                          !Needed for the G95 compiler.
      DATA ICALL/0/
C     DIMENSION VOLT(100).....now stored in clatic.for
C
      INCLUDE "cnlist.for"
      INCLUDE "clatic.for"
      INCLUDE "csol.for"
C
C
C
      PI=3.1415926535897932D0
      RADCON=8.85D-5/(2.D0*PI)
      IF(ICALL.EQ.1)GO TO 3
C
C
C
C
C=====CAVITY VOLTAGES & ENERGY LOSS.
      CAVOLT=0.D0
      RADLOS=0.D0
      IVOLT=0
      DO 1 I=1,NELEM
      ITY=ITYPE(I)
      IF(ID(ITY).EQ.5)CAVOLT=CAVOLT+XX(ITY)
      IF(ID(ITY).EQ.2.OR.ID(ITY).EQ.9.OR.ID(ITY).EQ.15.OR.ID(ITY).EQ.16)
     +                                  RADLOS=RADLOS+XX(ITY)**2/YY(ITY)
    1 CONTINUE
CCC      RADFAC = RADLOS    !Dec/05: What was this


      IF(CAVOLT.GT.0.D0)GO TO 2
   15 WRITE(53,10)
   10 FORMAT(' ',' TOTAL CAVITY VOLTAGE IS ZERO---SO STOP')
      IF(ISECT.GT.0)WRITE(53,31)
   31 FORMAT(' ','UNLESS JUST CHECKING A SECTION OF LATTICE')
      IF(ISECT.GT.0)RETURN
      STOP
    2 CONTINUE
      RADLOS=RADLOS*RADCON*ECAV**4*1000.D0
      SINPHI=RADLOS/CAVOLT
      PHI=DASIN(SINPHI)*180.D0/PI
      WRITE(53,11)ECAV,RADLOS,PHI
   11 FORMAT(' AT ENERGY OF      ',F10.4,' GEV :',/,
     +        ' RADN LOSS      =  ',F10.4,' MEV',/,
     +        ' PHASE          =  ',F10.4,' DEG.')
CCC     +        ' M-C RAD FACTOR =  ',F10.4)
C
C
C
C
C
    3 ENFAC=(E0/ECAV)**4
      U0=RADLOS*ENFAC
      CRAD=8.85D-5*E0**3/(2.D0*PI)
C
C
C
C=====SPIN THROUGH ELEMENTS TO DEFINE CAVITY STRENGTHS
C
      IF(ICALL.EQ. 0 )WRITE(53,12)
   12 FORMAT(' ',//,' CAVITY STRENGTHS'//,' CAVITY   COS(PHI) TERM   ',
     +                'SIN(PHI) TERM    PHASE(DEG)  TYPE')
C
      DO  4 ITY=1,NTYPE
      IID=ID(ITY)
      GOTO (4,4,4,4,5,4,4,4,4,4,4,4,4,4,4,4,4),IID
      GO TO 4
    5 IF(ICALL.NE.0)GO TO 6
      VOLT(ITY)=XX(ITY)
C     XX(ITY)=ENFAC*VOLT(ITY)*DSQRT(1.D0-SINPHI*SINPHI)*IHARM/E0
C=====DEFINE XX AT ENERGY ECAV, SO AS TO BE ABLE TO GET SAME COS TERM
C=====AS IN A PREVIOUS SCAN WHICH STARTED AT LOWER ENERGY.
C=====BUG FOUND AND FIXED: 15 SEPT 95. NOW SAME AS SLIM.
      XX(ITY)=      VOLT(ITY)*DSQRT(1.D0-SINPHI*SINPHI)*IHARM/ECAV
      XX(ITY)=XX(ITY)*2.*PI/RLENGE*0.001D0
C
C
      TMAT(6,5,ITY)=XX(ITY)
    6 CONTINUE
      YY(ITY)=ENFAC*VOLT(ITY)*SINPHI/E0*0.001D0
      IF(XX(ITY).GT.0.D0)TANPHI=YY(ITY)/XX(ITY)*IHARM*2.D0*PI/RLENGE
      PHI2=DATAN(TANPHI)*180.D0/PI
      IF(ICALL.EQ. 0 )WRITE(53,13)NAME(ITY),XX(ITY),YY(ITY),PHI2,ITY
   13 FORMAT(' ',3X,A4,F12.6,4X,F13.7,3X,F10.2,6X,I2)
C
C
   4  CONTINUE
C
C
      ICALL=1
      RETURN
      END
